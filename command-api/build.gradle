plugins {
    id 'java'
    id 'groovy' // Necessary to compile and run Spock Groovy tests
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.diffplug.spotless" version "${spotlessVersion}"
}

apply from: '../gradle/spotless.gradle'

group = 'com.rangerforce'
version = '1.0.0'

java {
    sourceCompatibility = "${javaVersion}"
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // SpringBoot
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // CQRS Core
    implementation project(":cqrs-core")
    implementation project(":bank-common")

    // Spring Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // Spock Framework
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
}

test {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "passed", "skipped", "failed"
            exceptionFormat = "full"
            showCauses = true
            showExceptions = true
            showStackTraces = false
            showStandardStreams = false // true -> will display all info regarding failing tests
        }
        info.events = lifecycle.events
        info.exceptionFormat = lifecycle.exceptionFormat
    }
}
